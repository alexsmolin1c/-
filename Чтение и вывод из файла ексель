https://open-budget.ru/public/1050198/?ysclid=lwg7293c6z403771595

Получение таблицы значений из excel в 1С v8, без COM, внешних источников данных и т.д.
EXCELВТаблицуЗначений() — За 10 строчек кода!
Реализация протестирована на 1С 8.3.12.1714 (x64).
WARNING

Данная статья не претендует на оригинальность и не является конечным решением.

Подходы решения задач и примеры программного кода несут исключительно обучающий характер.

Существует масса подходов загрузки данных из EXCEL в 1С. Большинство решений основаны на COM или еще более сложных возможностях платформы.

Для себя разработал наиболее оптимальный вариант:
EXCEL -> Табличный документ -> Построитель запроса -> Таблица значений

Итак, приступим:

Прочитать Excel файл на клиенте, для передачи на сервер: 
&НаКлиенте
Процедура ЗагрузитьEXCEL(Команда)

Файл = Новый Файл(ПутьКEXCEL);
Если НЕ Файл.Существует() Тогда
Возврат;
КонецЕсли;
ДвоичныеДанные = Новый ДвоичныеДанные(ПутьКEXCEL);
ДокументРезультат = EXCELВТаблицуЗначений(ДвоичныеДанные, Файл.Расширение);
ДокументРезультат.Показать();

КонецПроцедуры
 

Выполнить чтение Excel в таблицу значений: 
&НаСервере
Функция EXCELВТаблицуЗначений(ДвоичныеДанные, Расширение)

ФайлEXCELНаСервере = ПолучитьИмяВременногоФайла(Расширение);
ДвоичныеДанные.Записать(ФайлEXCELНаСервере);

ТабличныйДокумент = Новый ТабличныйДокумент;
ТабличныйДокумент.Прочитать(ФайлEXCELНаСервере);
УдалитьФайлы(ФайлEXCELНаСервере);
ОбластьТаблицы = ТабличныйДокумент.Область(8, 1, ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы);

ПостроительЗапроса = Новый ПостроительЗапроса;
ПостроительЗапроса.ИсточникДанных = Новый ОписаниеИсточникаДанных(ОбластьТаблицы);
ПостроительЗапроса.Выполнить();
ТаблицаДанных = ПостроительЗапроса.Результат.Выгрузить();

// теперь поссмторим что в Таблице
Возврат ТаблицаЗначенийВТабличныйДокумент(ТаблицаДанных);

КонецФункции 
 

Визуализировать таблицу значений: 

&НаСервереБезКонтекста
Функция ТаблицаЗначенийВТабличныйДокумент(ТаблицаДанных)

ПостроительОтчета = Новый ПостроительОтчета;
ПостроительОтчета.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТаблицаДанных);
ДокументРезультат = Новый ТабличныйДокумент;
Построительотчета.Вывести(ДокументРезультат);
Возврат ДокументРезультат;

КонецФункции
 

Описание преобразования данных:

Получаем исходный файл на клиенте;
На основании файла создаем двоичные данные и получаем расширение файла;
Передаем двоичные данные на сервер;
Создаем дубль файла excel на сервере;
Читаем файл в новый табличный документ;
Получаем область табличного документа, где находятся полезные данные (ТабличныйДокумент.Область(8, 1, ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы));
Создаем новый построитель запроса и установим в качестве источника данных — область полезных данных;
Выполним построитель и получим результат запроса;
Результат запроса выгрузим в таблицу значений.
Плюсы реализации:

Скорость работы по сравнению с COM EXCEL
Возможно выполнять загрузку где EXCEL нет вообще (MAC | LINUX | etc…)
Простота реализации
